
apply plugin: 'java'

apply plugin: 'findbugs'

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    excludeFilter = file("${projectDir}/config/findbugs/excludeFilter.xml")
}

dependencies {
    compile files('lib/java-cup-11b-runtime.jar')
}

String cupSpec = "$projectDir/grammar/parser.cup"
String jFlexSpec = "$projectDir/grammar/lexer.flex"

String cupLib = "$projectDir/lib/java-cup-11b.jar"
String jFlexLib = "$projectDir/lib/JFlex.jar"

String cupDest = "$projectDir/src/main/java/"
String jFlexDest = "$projectDir/src/main/java/"

task runJFlex(type:Exec) {
    commandLine 'java', '-jar', jFlexLib, jFlexSpec, '-d', jFlexDest
    standardOutput = new ByteArrayOutputStream()
    doLast {
        println standardOutput
    }
}

task runJavaCup(type:Exec) {
    commandLine 'java', '-jar', cupLib, '-destdir', cupDest, cupSpec
    standardOutput = new ByteArrayOutputStream()
    doLast {
        println standardOutput
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'Rectangle'
    }
}

compileJava.dependsOn runJFlex
compileJava.dependsOn runJavaCup
